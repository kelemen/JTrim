apply plugin: 'base'; // To add "clean" task to the root project.
apply from: rootProject.file('jtrim-version.gradle');

ext.displayName = 'JTrim';

ext.scriptFile = { String fileName ->
    return new File(new File(rootDir, "common-scripts"), fileName);
}

apply from: scriptFile('release.gradle');

task javadoc(type: Javadoc, description: 'Creates Javadoc from all the projects.') {
    title = 'JTrim - All modules';
    destinationDir = new File(project.buildDir, 'merged-javadoc');

    // Note: The closures below are executed lazily.
    source {
       subprojects*.sourceSets*.main*.allSource;
    }
    classpath.from {
        subprojects*.configurations*.compile*.copyRecursive({ !(it instanceof ProjectDependency); })*.resolve();
    }
}

task wrapper(type: Wrapper, description: 'Creates and deploys the Gradle wrapper to the current directory.') {
    gradleVersion = '1.4';
}
