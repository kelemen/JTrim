String mavenGroupId = 'org.jtrim';
String mavenVersion = '1.3-experimental';

subprojects {
    apply plugin: 'java';
    apply plugin: 'maven';

    String mavenArtifactId = name;

    group = mavenGroupId;
    version = mavenVersion;

    task sourcesJar(type: Jar, dependsOn: classes, description: 'Creates a jar from the source files.') {
        classifier = 'sources';
        from sourceSets.main.allSource;
    }

    artifacts {
        archives jar
        archives sourcesJar
    }

    configure(install.repositories.mavenInstaller) {
        pom.project {
            groupId = mavenGroupId;
            artifactId = mavenArtifactId;
            version = mavenVersion;
        }
    }
}

task mergedJavadoc(type: Javadoc, description: 'Creates Javadoc from all the projects.') {
    def thisTask = it;
    project.subprojects {
        thisTask.source(sourceSets.main.allSource);
    }

    title = 'JTrim - All modules';
    destinationDir = new File(project.buildDir, 'merged-javadoc');
}

task wrapper(type: Wrapper, description: 'Creates and deploys the Gradle wrapper to the current directory.') {
    gradleVersion = '1.0';
}
