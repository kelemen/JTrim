rootProject.name = 'jtrim'

enableFeaturePreview('VERSION_CATALOGS')

dependencyResolutionManagement {
    versionCatalogs {
        libs {
            version('java', '8')

            version('checkstyle', '9.2.1')
            version('jacoco', '0.8.7')

            alias('junit').to('junit:junit:4.13.2')
            alias('mockitoCore').to('org.mockito:mockito-core:1.10.19')

            bundle('testLibs', ['junit', 'mockitoCore'])
        }
    }
}

FileFilter projectDirFilter = new FileFilter() {
    public boolean accept(File dir) {
        return dir.isDirectory() && new File(dir, 'build.gradle').exists()
    }
}

def addChildren = { subprojectsRootDir ->
    getChildren(subprojectsRootDir, projectDirFilter).each { File dir ->
        String subprojectName = dir.name
        include subprojectName
        project(':' + subprojectName).projectDir = dir
    }
}

addChildren(new File(rootDir, 'subprojects'))
addChildren(new File(rootDir, 'subprojects-internal'))

private File[] getChildren(File root, FileFilter filter) {
    File[] children = root.listFiles(filter)
    return children != null ? children : new File[0]
}
