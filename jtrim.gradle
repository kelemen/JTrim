//
// This file is to be applied to each JTrim projects.
//

apply plugin: 'java';
apply plugin: 'maven';

String mavenGroupId = 'org.jtrim';
String mavenVersion = '1.3-experimental';

sourceCompatibility = '1.7';
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8';

String mavenArtifactId = name;

group = mavenGroupId;
version = mavenVersion;

task sourcesJar(type: Jar, dependsOn: classes, description: 'Creates a jar from the source files.') {
    classifier = 'sources';
    from sourceSets.main.allSource;
}

artifacts {
    archives jar
    archives sourcesJar
}

configure(install.repositories.mavenInstaller) {
    pom.project {
        groupId = mavenGroupId;
        artifactId = mavenArtifactId;
        version = mavenVersion;
    }
}

repositories {
    mavenLocal();
    mavenCentral();
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.10';
}
