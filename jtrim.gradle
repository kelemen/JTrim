//
// This file is to be applied to each JTrim projects.
//

apply plugin: 'java';
apply plugin: 'maven';

ext.scriptFile = { String fileName ->
    return new File(new File(rootDir, "common-scripts"), fileName);
}

if (!hasProperty('codeCoverage')) ext.codeCoverage = '';
if (codeCoverage == null) codeCoverage = '';
codeCoverage = codeCoverage.toString().trim();

if (!codeCoverage.isEmpty()) {
    apply from: scriptFile(codeCoverage + '.gradle');
}

String mavenGroupId = 'org.jtrim';
String mavenVersion = '1.3.3-SNAPSHOT';

sourceCompatibility = '1.7';

def compilerOptions = [compileJava, compileTestJava]*.options;
compilerOptions*.encoding = 'UTF-8';
compilerOptions*.compilerArgs = ['-Xlint'];

String mavenArtifactId = name;

group = mavenGroupId;
version = mavenVersion;

task sourcesJar(type: Jar, dependsOn: classes, description: 'Creates a jar from the source files.') {
    classifier = 'sources';
    from sourceSets.main.allSource;
}

artifacts {
    archives jar
    archives sourcesJar
}

afterEvaluate {
    System.setProperty('line.separator', '\n');
}

configure(install.repositories.mavenInstaller) {
    pom.project {
        groupId = mavenGroupId;
        artifactId = mavenArtifactId;
        version = mavenVersion;
    }
}

if (project.hasProperty('deployPath')) {
    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: deployPath);
            }
        }
    }
}

repositories {
    mavenLocal();
    mavenCentral();
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.10';
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.9.0';
}
